apply plugin: 'java'

repositories {
    jcenter()
}

dependencies {
    runtimeOnly 'org.antlr:antlr4:4.9.3'
}

task generateLexer(type:JavaExec) {
  def lexerName = "hclLexer"
  inputs.file("$ANTLR_SRC/${lexerName}.g4")
  outputs.file("$ANTLR_SRC/${lexerName}.js")
  outputs.file("$ANTLR_SRC/${lexerName}.interp")
  outputs.file("$ANTLR_SRC/${lexerName}.tokens")
  main = 'org.antlr.v4.Tool'
  classpath = sourceSets.main.runtimeClasspath
  args = ['-Dlanguage=JavaScript', "${lexerName}.g4", '-o', '../../schemas_terraform']
  workingDir = ANTLR_SRC   
}

task generateParser(type:JavaExec) {
  dependsOn generateLexer
  def lexerName = "hclLexer"
  def parserName = "hclParser"
  inputs.file("$ANTLR_SRC/${parserName}.g4")
  inputs.file("$ANTLR_SRC/${lexerName}.tokens")
  outputs.file("$ANTLR_SRC/${parserName}.js")
  outputs.file("$ANTLR_SRC/${parserName}.interp")
  outputs.file("$ANTLR_SRC/${parserName}.tokens")
  main = 'org.antlr.v4.Tool'
  classpath = sourceSets.main.runtimeClasspath
  args = ['-Dlanguage=JavaScript', "${parserName}.g4", '-no-listener', '-no-visitor', '-o', '../../schemas_terraform']
  workingDir = ANTLR_SRC
}